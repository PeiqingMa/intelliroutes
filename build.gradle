import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jetbrains.grammarkit.tasks.GenerateParser

buildscript {
    repositories {
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.github.hurricup:gradle-grammar-kit-plugin:2018.2.2'
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.20'
    id 'org.jetbrains.intellij' version '0.4.2'
    id 'com.palantir.git-version' version '0.11.0'
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'io.kotlintest:kotlintest:2.0.7'
}

group 'com.github.tomas-milata'
version gitVersion() // uses git describe

sourceCompatibility = 1.8
targetCompatibility = 1.8
apply plugin: 'org.jetbrains.grammarkit'

def genRoot = file('src/generated')

sourceSets {
    main {
        java.srcDirs 'src/main', genRoot
        resources.srcDir 'resources'
    }
}

intellij {
    version 'LATEST-EAP-SNAPSHOT'
    plugins 'org.intellij.scala:2019.1.1'
}

patchPluginXml {
    sinceBuild '191'
}

task generateParser(type: GenerateParser) {
    source = 'src/main/grammar/Routes.bnf'
    pathToParser = '/com/github/tomasmilata/intelliroutes/parser/RoutesParser.java'
    pathToPsiRoot = '/com/github/tomasmilata/intelliroutes/psi'
    targetRoot = genRoot
    purgeOldFiles = true
}

task generateLexer(type: GenerateLexer) {
    dependsOn generateParser
    source = 'src/main/jflex/Routes.flex'
    targetDir = "${genRoot}/com/github/tomasmilata/intelliroutes"
    targetClass = 'RoutesLexer'
    purgeOldFiles = true
}

compileKotlin {
    dependsOn generateLexer
}